
plugins {
	id 'de.undercouch.download' version '1.2'
	id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'base'
apply plugin: 'maven-publish'

import de.undercouch.gradle.tasks.download.Download

task downloadArchive(type: Download) {
    src "http://download.java.net/openjdk/jdk7u75/ri/openjdk-7u75-b13-windows-i586-18_dec_2014.zip"
    dest buildDir
}

task extractArchive(type: Copy, dependsOn: downloadArchive) {
    from { zipTree("$buildDir/openjdk-7u75-b13-windows-i586-18_dec_2014.zip") }
    into "$buildDir/files"
}

task renameDirectory() << {
    file("$buildDir/files/java-se-7u75-ri").renameTo(file("$buildDir/files/openjdk-7u75"))
}

extractArchive.finalizedBy renameDirectory

task customize(type: Copy, dependsOn: extractArchive) {
	from "files"
	into "$buildDir/files"
}

task buildPackage(type: Jar, dependsOn: customize) {
	baseName project.name
	version version
	extension 'jar'
	destinationDir buildDir
	from "$buildDir/files"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}

publishing {
    publications {
        openJdk7(MavenPublication) {
			artifact "${buildDir}/${project.name}-${version}.jar"
		}
    }
	repositories {
		// set the properties via -P to publish to your company repo
		maven {  			
			url = project.hasProperty('nexusUrl') ? project.nexusUrl : ''
			credentials {
				username = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
				password = project.hasProperty('nexusPassword') ? project.nexusPassword : ''
			}			
		}
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : 'unknown'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'unknown'
    publications = ['openJdk7']
	publish = true
	dryRun = false
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description		
        licenses = ['GPL-2.0+CE']
        labels = ['openjdk', 'seu-as-code', 'package']
		websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        
        version {
            name = project.version
			desc = "${project.description} ${project.version}"
			released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
			
			mavenCentralSync {
				sync = false
			}
        }	
    }
}