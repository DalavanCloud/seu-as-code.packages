
plugins {
	id 'de.undercouch.download' version '1.2'
	id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'base'
apply plugin: 'maven-publish'

import de.undercouch.gradle.tasks.download.Download

task downloadArchive(type: Download) {
    src "https://download.elastic.co/logstash/logstash/logstash-1.5.3.zip"
    dest buildDir
}

task extractArchive(type: Copy, dependsOn: downloadArchive) {	
	from { zipTree("$buildDir/logstash-1.5.3.zip") }
	into "$buildDir/files"
}

task customizePackage(type: Copy, dependsOn: extractArchive) {
    from "files"
    into "$buildDir/files"
}

task buildPackage(type: Jar, dependsOn: customizePackage) {
	baseName project.name
	version version
	extension 'jar'
	destinationDir buildDir
	from "$buildDir/files"
}

publishing {
    publications {
		logstash(MavenPublication) {
			artifact "${buildDir}/${project.name}-${version}.jar"
		}
    }
	repositories {
		// set the properties via -P to publish to your company repo
		maven {  			
			url = project.hasProperty('nexusUrl') ? project.nexusUrl : ''
			credentials {
				username = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
				password = project.hasProperty('nexusPassword') ? project.nexusPassword : ''
			}			
		}
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : 'unknown'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'unknown'
    publications = ['logstash']
	publish = true
	dryRun = false
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description		
        licenses = ['Apache-2.0']
        labels = ['logstash', 'seu-as-code', 'package']
		websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        
        version {
            name = project.version
			desc = "${project.description} ${project.version}"
			released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
			
			mavenCentralSync {
				sync = false
			}
        }	
    }
}